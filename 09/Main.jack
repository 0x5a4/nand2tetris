class Main {
    static int selectionX;
    static int selectionY;
    
    function void main() {
        var int seed;
        do Random.initTable();
        do Grid.init();

        let seed = Main.showTutorial();

        while (true) {
            do Grid.generate(seed);
            do Graphics.drawUI();
            do Graphics.drawAll();

            let seed = Main.gameLoop();
            do Screen.clearScreen();
        }
        
        return;
    }

    function int gameLoop() {
        var int keycode;
        var int lastKeycode;
        var int revealResult;
        var int timer;

        let selectionX = 8;
        let selectionY = 8;

        while (revealResult = 0) {
            do Graphics.draw(selectionX, selectionY, true);

            while (keycode = lastKeycode) {
                let keycode = Keyboard.keyPressed();
            }

            let lastKeycode = keycode;

            do Graphics.clear(selectionX, selectionY);
            do Graphics.draw(selectionX, selectionY, false);
            
            // left 
            if (lastKeycode = 130) {
                do Main.moveSelection(selectionX - 1, selectionY);
            }
            
            // right
            if (lastKeycode = 132) {
                do Main.moveSelection(selectionX + 1, selectionY);
            }
            
            // up
            if (lastKeycode = 131) {
                do Main.moveSelection(selectionX, selectionY - 1);
            }
            
            // down
            if (lastKeycode = 133) {
                do Main.moveSelection(selectionX, selectionY + 1);
            }

            // flagging
            if (lastKeycode = 32) {
                do Grid.toggleFlag(selectionX, selectionY);
                do Graphics.drawFlagCounter();
            }
            
            // reveal
            if (lastKeycode = 128) {
                let revealResult = Grid.revealWithSurrounding(selectionX, selectionY);

                do Graphics.drawAll();
                
                if (revealResult = 1) {
                    do Graphics.win();
                }
                
                if (revealResult = 2) {
                    do Graphics.loose();
                }
            }
        }

        do Grid.revealAll();
        do Graphics.drawAll();

        do Graphics.drawContinuePrompt();

        while ((keycode = lastKeycode)) {
            let timer = timer + 1;
            let keycode = Keyboard.keyPressed();
            if (keycode = 0) {
                let keycode = lastKeycode;
            }
        }

        return timer;
    }

    function void moveSelection(int x, int y) {
        if ((x < 0) | (y < 0) | (x > 15) | (y > 15)) {
            return;
        }

        let selectionX = x;
        let selectionY = y;

        do Graphics.draw(x, y, true);

        return;
    }

    function int showTutorial() {
        var int timer;
        
        do Graphics.drawTutorial();

        while (Keyboard.keyPressed() = 0) {
            let timer = timer + 1;
        }
        
        do Screen.clearScreen();

        return timer;
    }
}
